-- ============================================================
-- Кредиты: пакет CRED_ADMIN_PKG (админка)
-- ============================================================

CREATE OR REPLACE PACKAGE CRED_ADMIN_PKG AS
  PROCEDURE GET_PROGRAMS (
    P_BANK   IN  VARCHAR2 DEFAULT NULL,
    P_TERM   IN  NUMBER   DEFAULT NULL,
    P_ACTIVE IN  VARCHAR2 DEFAULT NULL,
    P_CUR    OUT SYS_REFCURSOR
  );
  PROCEDURE GET_PROGRAM_BY_ID (
    P_ID     IN  NUMBER,
    P_CUR    OUT SYS_REFCURSOR
  );
  PROCEDURE UPSERT_PROGRAM (
    P_ID              IN OUT NUMBER,
    P_NAME            IN     VARCHAR2,
    P_BANK_ID         IN     NUMBER,
    P_TERM_MONTHS     IN     NUMBER,
    P_RATE_PCT        IN     NUMBER DEFAULT 0,
    P_FIRST_PAYMENT   IN     NUMBER DEFAULT 0,
    P_MIN_SUM         IN     NUMBER,
    P_MAX_SUM         IN     NUMBER,
    P_COMMISSION_PCT  IN     NUMBER DEFAULT 0,
    P_ACTIVE          IN     VARCHAR2 DEFAULT 'Y',
    P_NOTES           IN     VARCHAR2 DEFAULT NULL
  );
  PROCEDURE DELETE_PROGRAM (P_ID IN NUMBER);
  PROCEDURE GET_MATRIX (P_CUR OUT SYS_REFCURSOR);
  PROCEDURE SET_MATRIX_ROW (
    P_PROGRAM_ID  IN NUMBER,
    P_CATEGORY_ID IN NUMBER,
    P_ENABLED     IN NUMBER
  );
END CRED_ADMIN_PKG;
/

CREATE OR REPLACE PACKAGE BODY CRED_ADMIN_PKG AS
  PROCEDURE GET_PROGRAMS (
    P_BANK   IN  VARCHAR2 DEFAULT NULL,
    P_TERM   IN  NUMBER   DEFAULT NULL,
    P_ACTIVE IN  VARCHAR2 DEFAULT NULL,
    P_CUR    OUT SYS_REFCURSOR
  ) IS
  BEGIN
    OPEN P_CUR FOR
      SELECT ID, NAME, BANK_NAME, BANK_ID, TERM_MONTHS, RATE_PCT, MIN_SUM, MAX_SUM,
             FIRST_PAYMENT_PCT, COMMISSION_PCT, ACTIVE, NOTES
      FROM V_CRED_PROGRAMS
      WHERE (P_BANK IS NULL OR BANK_CODE = P_BANK)
        AND (P_TERM IS NULL OR TERM_MONTHS = P_TERM)
        AND (P_ACTIVE IS NULL OR (P_ACTIVE = 'Y' AND ACTIVE = 'Y') OR (P_ACTIVE = 'N' AND ACTIVE = 'N'))
      ORDER BY BANK_NAME, TERM_MONTHS, ID;
  END GET_PROGRAMS;

  PROCEDURE GET_PROGRAM_BY_ID (
    P_ID     IN  NUMBER,
    P_CUR    OUT SYS_REFCURSOR
  ) IS
  BEGIN
    OPEN P_CUR FOR
      SELECT ID, NAME, BANK_ID, TERM_MONTHS, RATE_PCT, FIRST_PAYMENT_PCT,
             MIN_SUM, MAX_SUM, COMMISSION_PCT, ACTIVE, NOTES
      FROM CRED_PROGRAMS WHERE ID = P_ID;
  END GET_PROGRAM_BY_ID;

  PROCEDURE UPSERT_PROGRAM (
    P_ID              IN OUT NUMBER,
    P_NAME            IN     VARCHAR2,
    P_BANK_ID         IN     NUMBER,
    P_TERM_MONTHS     IN     NUMBER,
    P_RATE_PCT        IN     NUMBER DEFAULT 0,
    P_FIRST_PAYMENT   IN     NUMBER DEFAULT 0,
    P_MIN_SUM         IN     NUMBER,
    P_MAX_SUM         IN     NUMBER,
    P_COMMISSION_PCT  IN     NUMBER DEFAULT 0,
    P_ACTIVE          IN     VARCHAR2 DEFAULT 'Y',
    P_NOTES           IN     VARCHAR2 DEFAULT NULL
  ) IS
  BEGIN
    IF P_ID IS NULL OR P_ID = 0 THEN
      INSERT INTO CRED_PROGRAMS (
        NAME, BANK_ID, TERM_MONTHS, RATE_PCT, FIRST_PAYMENT_PCT,
        MIN_SUM, MAX_SUM, COMMISSION_PCT, ACTIVE, NOTES
      ) VALUES (
        P_NAME, P_BANK_ID, P_TERM_MONTHS, P_RATE_PCT, P_FIRST_PAYMENT,
        P_MIN_SUM, P_MAX_SUM, P_COMMISSION_PCT, P_ACTIVE, P_NOTES
      )
      RETURNING ID INTO P_ID;
    ELSE
      UPDATE CRED_PROGRAMS SET
        NAME = P_NAME, BANK_ID = P_BANK_ID, TERM_MONTHS = P_TERM_MONTHS,
        RATE_PCT = P_RATE_PCT, FIRST_PAYMENT_PCT = P_FIRST_PAYMENT,
        MIN_SUM = P_MIN_SUM, MAX_SUM = P_MAX_SUM, COMMISSION_PCT = P_COMMISSION_PCT,
        ACTIVE = P_ACTIVE, NOTES = P_NOTES
      WHERE ID = P_ID;
    END IF;
    COMMIT;
  END UPSERT_PROGRAM;

  PROCEDURE DELETE_PROGRAM (P_ID IN NUMBER) IS
  BEGIN
    DELETE FROM CRED_PROGRAMS WHERE ID = P_ID;
    COMMIT;
  END DELETE_PROGRAM;

  PROCEDURE GET_MATRIX (P_CUR OUT SYS_REFCURSOR) IS
  BEGIN
    OPEN P_CUR FOR SELECT * FROM V_CRED_MATRIX;
  END GET_MATRIX;

  PROCEDURE SET_MATRIX_ROW (
    P_PROGRAM_ID  IN NUMBER,
    P_CATEGORY_ID IN NUMBER,
    P_ENABLED     IN NUMBER
  ) IS
  BEGIN
    IF P_ENABLED = 1 THEN
      INSERT INTO CRED_PROGRAM_CATEGORIES (PROGRAM_ID, CATEGORY_ID)
      SELECT P_PROGRAM_ID, P_CATEGORY_ID FROM DUAL
      WHERE NOT EXISTS (
        SELECT 1 FROM CRED_PROGRAM_CATEGORIES
        WHERE PROGRAM_ID = P_PROGRAM_ID AND CATEGORY_ID = P_CATEGORY_ID
      );
    ELSE
      DELETE FROM CRED_PROGRAM_CATEGORIES
      WHERE PROGRAM_ID = P_PROGRAM_ID AND CATEGORY_ID = P_CATEGORY_ID;
    END IF;
    COMMIT;
  END SET_MATRIX_ROW;
END CRED_ADMIN_PKG;
/
